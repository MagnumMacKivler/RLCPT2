@name magspack2/tcl/class_splight_system/TCL Tri-Beam Class Splight Controller
@inputs Pod:entity NextWeapon PrevWeapon ThirdPerson
@outputs RGB:vector SwitchPos
@persist E:entity Driver:entity Position Unlock KeyUnlock SwitchSound:string UnlockKey:string Radius
@persist Name:string
@trigger NextWeapon PrevWeapon UnlockKey

#V3.0 - Now using Anyone's new model. Also added key-based unlocking like the Headlight Controller!

#-------------------------------------------------------------------------------#
# W E L C O M E   A B O A R D   T H E   T R A N S - C A N A D A   L I M I T E D #
#                                                                               #
# Hello and welcome to TCL's Advanced Class Splight Controller! This type of    #
# rotary switch can be found in pretty much any mid- to late-era diesel that    #
# has tri-beam (white/red/green) class lights.                                  #
#                                                                               #
# This controller E2 is designed to work in tandem with the TCL Advanced        #
# Tri-Beam Class Splight system.                                                #
#                                                                               #
# Setup:                                                                        #
#   1)  Spawn and position this E2 somewhere accessible from your engineer's    #
#       seat. ** It must be visible from the seat to be used. **                #
#   2)  Spawn, position and parent your Class Splight E2s - see their code for  #
#       any special instructions.                                               #
#   3)  Wire all inputs on this E2 to their respective outputs on your Pod.     #
#   4)  Wire the SwitchPos output to your Class Splight E2s.                    #
#   5)  Cycle through the positions by aiming at the E2 and rolling your mouse  #
#       wheel up and down.                                                      #
#   6)  Make sure you parent everything, and enjoy!                             #
#                                                                               #
#   Tip- Not sure where to place your class light switches? Older Canadian      #
#   locomotives usually have a small extension built onto the left of the       #
#   control stand, directly adjacent to the brake controls, where the light     #
#   controls can be found. More modern locomotives with desktop stands have     #
#   them positioned overhead airliner style, or on the front face of the        #
#   console underneath the Engine Run/Gen Field/Fuel Pump triple switches.      #
#                                                                               #
# SUPER thanks to Monkatraz for help with development and optimization!         #
#                                                                               #
# Happy railroading!                                                     TCL <3 #
#                                                                               #
#         Having problems? Find me on Discord!  TransCanadaLimited#8427         #
#-------------------------------------------------------------------------------#

if(first() | dupefinished()){
    
SwitchSound = "trans_train_sounds/buttons/headlight_switch.wav"

UnlockKey = "F" #Hold down this key to operate the switch without looking at it.

Name = "Class Lights" #"Front Class", "Rear Right Class", etc

SwitchScale = 1 #Scale of the switch model.

Radius = 2 #Radius your cursor will be detected from.

####

E=entity()
if(E:parent():isValid()){
        E:setMaterial("models/proppertextures/invisible")
    }

holoCreate(0)
holoParent(0,E)
holoModel(0,"models/anytrains/props/detail/class_light_switch.mdl")
holoScale(0,vec(SwitchScale))

timer("clk",100)

}

#Main Code

Driver = Pod:driver()
if(Driver){
Unlock = Driver:aimPos():distance(E:pos()) < Radius
KeyUnlock = Driver:keyPressed(UnlockKey)
}

if(inputClk() & ((Unlock & !ThirdPerson) | KeyUnlock)){
    if(PrevWeapon){ #Switch Pos Up
        soundPlay(0,1,SwitchSound)
        if(Position<3){
            Position++
        }elseif(Position==3){
            Position=0
        }
    }elseif(NextWeapon){ #Switch Pos Down
        soundPlay(0,1,SwitchSound)
        if(Position>0){
            Position--
        }elseif(Position==0){
            Position=3
        }
    }
}

#All lights off
if(Position==0){
    holoBodygroup(0,2,0)
    RGB = vec(0)
    SwitchPos = 0
    setName(Name+"\n(Off)")
}

#Colour 1 (White)
if(Position==1){
    holoBodygroup(0,2,1)
    RGB = vec(200)
    SwitchPos = 1
    setName(Name+"\n(White)")
}

#Colour 2 (Green)
if(Position==2){
    holoBodygroup(0,2,2)
    RGB = vec(100,200,100)
    SwitchPos = 3
    setName(Name+"\n(Green)")
}

#Colour 3 (Red)
if(Position==3){
    holoBodygroup(0,2,3)
    RGB = vec(200,0,0)
    SwitchPos = 2
    setName(Name+"\n(Red)")
}
