@name magspack2/tcl/cstands_bg_tcl/cstand_sd60m_desktop_tcl
@inputs MU_Master Pod:entity ControlStand:wirelink Number:string Horn1 ForceSand EmBrake BellToggle LightToggle MiscToggle HEP_Notch Active RemoteControl UsingRearSeat HumpMultiplier
@outputs CtlTable:table Bell Light Misc CONSTANT RemoteMode IncreaseThrottle DecreaseThrottle IncreaseDynamics DecreaseDynamics
@persist NotchedThrottle NumThrottleNotches HasDynamics NotchedDynamics TransitionMode TransitionControlType AirBrakeMode LocoBrakeMode InitialReduction Lock_26L Bleeding CTL_Version:string
@persist [Lever_PowerHandleCrossover Lever_PowerHandleNotch Lever_PowerHandleIdle Lever_PowerHandleMax Lever_PowerHandleDBMax]:string
@persist [Lever_ReverserF Lever_ReverserN Lever_ReverserB]:string
@persist [Lever_ABEmergency Lever_ABEmergencyRecover Lever_ABSuppressionOn Lever_ABSuppressionOff Lever_ABMove Lever_ABNotch Lever_ABRelease]:string
@persist [AirBrakeApply AirBrakeApplyInitial AirBrakeRelease AirBrakeDump]:string
@persist [LocoBrakeFull LocoBrakeReleaseHard LocoBrakeReleaseSoft]:string
@persist [ButtonMomOn ButtonMomOff ButtonTogOn ButtonTogOff HornAirOn HornAirOff WarningHiss]:string
@persist [BellValveOn BellValveOff BellTriggerOn MiscSwitch]:string
@persist BellTrigger
@persist NQ Notch DNQ DynaNotch Dynamics TransitionQ RevQ ABQ HissQ LBQ LHissQ
@persist ControlPrintOrder:array DriverQ Routing:table DriverOld:entity Controls:table Shift:string
@persist ReverserF ReverserB IncreaseThrottle DecreaseThrottle KeyThrUp KeyThrDn KeyDynUp KeyDynDn IncreaseDynamics DecreaseDynamics IncreaseTrainBrake DecreaseTrainBrake IncreaseLocoBrake DecreaseLocoBrake SelectorUp SelectorDown
@persist HandBrake EmergencyBrake Sander SanderQ
@persist SetReverser SetThrottle SetDynamics SetSelector SetBrakeNotch SetEqualRes SetLocoBrakeNotch SetLocoBrakePressure SetBleeding FeedPressure FullReleaseCock WarningHissQ
@persist CS:entity LeverBased Midshift BellQ HornQ
@trigger 
if(duped()){
    reset()
}
if(first()){
    #FOR USE WITH RLC PT 2.0.65 OR OTHER COMPATIBLE VERSIONS
    CtlTable["CS_Version",string] = "2.0.65"
    #HEAVILY MODIFIED, BUT FULLY COMPATIBLE WITH THIS VERSION
    
    #General Setup Instructions:
    #You will need one of these with RLC PT2. Spawn it somewhere and get wiring! See the RLC PT2 Operator's Manual for more instructions.
    #Don't neglect to check the config!
    #The "HEP_Notch" input is only necessary if the locomotive has HEP.
    #The "Active" and "RemoteControl" inputs are only necessary if the locomotive is equipped with Remote Control capability; "Active" goes to the Pod Controller, "RemoteControl" to a separate toggled button.
    #The "UsingRearSeat" input  is only necessary if the locomotive features Dual Cabs or Dual Control Stands.
	
	   #Modified by TransCanadaLimited.
    
    ### CONFIGURATION ###
    
    #Pick a Control Scheme File! You can find them in "ptgamma/controls/" !
    #include "ptgamma/controls/Standard"
    
    AirBrakeMode = 2 #For best results, leave at 2.
    #0 - Simple Self-Lapping (Easy)
    #1 - Simple Manual Lapping (Medium)
    #2 - 26L Self-Lapping (Medium-Hard)
    #3 - 24RL Manual Lapping (Hard)
    #4 - No. 6 (6SL/6BL/6DS/6ET) Manual Lapping (Hard)
    #24RL, No. 6, and 26L are the hardest, but also the most rewarding!
    
    LocoBrakeMode = 0 #0 for Self Lapping, 1 for Manual Lapping
    
    InitialReduction = 6 #If using 24RL or 26L brake modes, the train brake will automatically drop this much pressure in First Service or Initial Reduction positions.
    FeedPressure = 90 #Feed Valve pressure in PSI. 70 was the standard until the 1950s, 90 is the standard for freight. 110 is the passenger standard, 120 is sometimes used by mountain railroads.
    
    NotchedThrottle = 1 #0 for Continuous Throttle, 1 for Notched Throttle
    NumThrottleNotches = 8 #Number of Throttle Notches, not counting idle (If Used).
    
    HasDynamics = 1 #1 for Has Dynamic Brakes, 0 for Doesn't Have Dynamic Brakes
    
    NotchedDynamics = 0 #0 for Continuous Dynamic Brakes, 1 for Notched Dynamic Brakes
    
    TransitionMode = 1 #0 for No Transition, 1 for Transition
    TransitionControlType = 2 #0 for MANUAL Transition, 1 for Automatic Transition With Selector, 2 for Fully Automatic Transition
    
    BellTrigger = 1 #Set to 1 to have your horn input also turn the bell on.

    ### CONTROL SOUND PATHS ###
    
	   #Combined Power Handle (Throttle + Dynamics)
    Lever_PowerHandleDBMax = "trans_train_sounds/controls/desktop/powerhandle_dbmax.wav"
    Lever_PowerHandleCrossover = "trans_train_sounds/controls/desktop/powerhandle_crossover.wav"
    Lever_PowerHandleIdle = "trans_train_sounds/controls/desktop/powerhandle_notchdown.wav"
    Lever_PowerHandleNotch = "trans_train_sounds/controls/desktop/powerhandle_notchup.wav"
    Lever_PowerHandleMax = "trans_train_sounds/controls/desktop/powerhandle_notchupmax.wav"
    
	   #Reverser
    Lever_ReverserF = "trans_train_sounds/controls/desktop/reverser_forward.wav"      
    Lever_ReverserN = "trans_train_sounds/controls/desktop/reverser_center.wav"
    Lever_ReverserB = "trans_train_sounds/controls/desktop/reverser_reverse.wav"
    
	   #Automatic / Train Brake Lever (30CDW / Analog)
    Lever_ABEmergency = "trans_train_sounds/controls/desktop/30cdw_auto_emergency.wav"
    Lever_ABEmergencyRecover = "trans_train_sounds/controls/desktop/30cdw_auto_emergency_recover.wav"
    Lever_ABSuppressionOn = "trans_train_sounds/controls/desktop/30cdw_auto_suppression_on.wav"
    Lever_ABSuppressionOff = "trans_train_sounds/controls/desktop/30cdw_auto_suppression_off.wav"
    Lever_ABMove = "trans_train_sounds/controls/desktop/30cdw_auto_lever_move.wav"
    Lever_ABNotch = "trans_train_sounds/controls/desktop/30cdw_auto_lever_notch.wav"
    Lever_ABRelease = "trans_train_sounds/controls/desktop/30cdw_auto_lever_release.wav"
	
	   #Automatic / Train Brake Lever (ICE / Electronic)
	   #[
	   Lever_ABEmergency = "trans_train_sounds/controls/desktop/ice_auto_lever_emergency.wav"
    Lever_ABEmergencyRecover = "trans_train_sounds/controls/desktop/ice_auto_lever_notch.wav"
    Lever_ABSuppressionOn = "trans_train_sounds/controls/desktop/ice_auto_lever_notch.wav"
    Lever_ABSuppressionOff = "trans_train_sounds/controls/desktop/ice_auto_lever_notch.wav"
    Lever_ABMove = "trans_train_sounds/controls/desktop/ice_auto_lever_move.wav"
    Lever_ABNotch = "trans_train_sounds/controls/desktop/ice_auto_lever_notch.wav"
    Lever_ABRelease = "trans_train_sounds/controls/desktop/ice_auto_lever_release.wav"
	   ]#
    
	   #Automatic / Train Brake Air (30CDW / Analog)
    AirBrakeApply = "trans_train_sounds/controls/desktop/30cdw_auto_air_apply.wav"
    AirBrakeApplyInitial = "trans_train_sounds/controls/desktop/30cdw_auto_air_apply_initial.wav"
    AirBrakeRelease = "trans_train_sounds/controls/desktop/30cdw_auto_air_release.wav"
	
	   #Automatic / Train Brake Air (ICE / Electronic)
	   #[
	   AirBrakeApply = "trans_train_sounds/controls/desktop/ice_auto_air_apply.wav"
    AirBrakeApplyInitial = "trans_train_sounds/controls/desktop/ice_auto_air_apply.wav"
    AirBrakeRelease = "trans_train_sounds/controls/desktop/ice_auto_air_release.wav"
    AirBrakeDump = "trans_train_sounds/air/dump.wav"
	   ]#
    
	   #Independent / Locomotive Brake (30CDW / Analog)
    LocoBrakeFull = "trans_train_sounds/controls/desktop/30cdw_indy_full.wav"
    LocoBrakeReleaseSoft = "trans_train_sounds/controls/desktop/30cdw_indy_release_soft.wav"
    LocoBrakeReleaseHard = "trans_train_sounds/controls/desktop/30cdw_indy_release.wav"
	
	   #Independent / Locomotive Brake (ICE / Electronic)
	   #[
    LocoBrakeFull = "trans_train_sounds/controls/desktop/ice_indy_apply.wav"
    LocoBrakeReleaseSoft = "trans_train_sounds/controls/desktop/ice_indy_release_soft.wav"
    LocoBrakeReleaseHard = "trans_train_sounds/controls/desktop/ice_indy_release.wav"
	   ]#
	
	   #Buttons, misc
    ButtonMomOn = "monkssounds/buttons/button31_push.wav"
    ButtonMomOff = "monkssounds/buttons/button31_release.wav"
    
    HornAirOn = "gsgtrainsounds/misc/desktop/horn_air_loop.wav"
    HornAirOff = "gsgtrainsounds/misc/desktop/horn_air_release.wav"
    
    BellValveOn = "trans_train_sounds/air/ge_bellvalve_on.wav"
    BellValveOff = "trans_train_sounds/air/ge_bellvalve_off.wav"
    BellTriggerOn = "gsgtrainsounds/misc/air_on.wav"
    
    MiscSwitch = "buttons/lightswitch2.wav"
    
    WarningHiss = "gsgtrainsounds/misc/desktop/horn_air_loop.wav"
	
	   ### ADVANCED USERS ONLY BEYOND THIS POINT ###
    
    #SOUND CHANNELS
    #1 - control levers (throttle, reverser)
    #2 - air brake levers (auto, indy)
    #3 - air brake hisses
    #4 - buttons
    #5 - horn air
    #6 - bell air
    
    function vector linpos(V1:vector, V2:vector, P){
        return V1 + P*(V2-V1)
    }
    
    function normal adc(MinVal, MaxVal, Resolution, Input){
        local Output = 0
        if(inrange(Input,MinVal,MaxVal)){
            Output = round(Resolution*((Input - MinVal)/(MaxVal-MinVal)))
            
        }elseif(Input < MinVal){
            Output = 0
        }elseif(Input > MaxVal){
            Output = Resolution
        }
        
        return Output
    }
    
    function normal dac(MinVal, MaxVal, Resolution, Input){
        local Output = 0
        if(inrange(Input,0,Resolution)){
            Input = round(Input)
            Output = (MaxVal - MinVal)*(Input/Resolution) + MinVal
        }elseif(Input < 0){
            Output = MinVal
        }elseif(Input > Resolution){
            Output = MaxVal
        }
        return Output
    }
    
    function void entity:printProper(Message:string){
        if(This==owner()){
            print(Message)
        }else{
            Pod:printDriver(Message)
        }
    }
    
    
    
    ControlPrintOrder = array(
        "ReverserF",
        "ReverserB",
        "IncreaseThrottle",
        "DecreaseThrottle",
        "IncreaseDynamics",
        "DecreaseDynamics",
        "SelectorUp",
        "SelectorDown",
        "IncreaseTrainBrake",
        "DecreaseTrainBrake",
        "IncreaseLocoBrake",
        "DecreaseLocoBrake",
        "EmergencyBrake",
        "HandBrake",
        "Sander",
        "UseButtons"
    )
    
    #Controls Init
    foreach(K,V:string = Controls){
        if(V:find(" +Shift")){
            Routing[K,number] = 1
            local EXP = V:explode(" ")
            Controls[K,string] = EXP[1,string]
        }else{
            Routing[K,number] = 0
        }
    }
    
    
    CtlTable["AirBrakeMode",number] = AirBrakeMode
    CtlTable["InitialReduction",number] = InitialReduction
    CtlTable["NotchedThrottle",number] = NotchedThrottle
    CtlTable["NumThrottleNotches",number] = NumThrottleNotches
    CtlTable["NotchedDynamics",number] = NotchedDynamics
    CtlTable["TransitionMode",number] = TransitionMode
    CtlTable["TransitionControlType",number] = TransitionControlType
    
    if(AirBrakeMode==2){#26L
        SetBrakeNotch = 6
    }elseif(AirBrakeMode>=3){#24RL/6SL
        SetBrakeNotch = 6
        #print("Yarg")   
    }elseif(AirBrakeMode==1){#Simple ML
        SetBrakeNotch = 2
    }else{ #Simple SL
        SetBrakeNotch = 0
    }
    
    SetLocoBrakePressure = 60
    LocoBrakeNotch = 2
    
    CONSTANT = 1
    
    
    #Control Stand Init
    
    
    #Control Stand Init
    CS = ControlStand:entity()
    
    CtlTable["CTL_Version",string] = CTL_Version
    setName("RLC PT2 Control Stand ("+CtlTable["CS_Version",string]+")\nEMD SD60M Desktop\n"+select(AirBrakeMode+1,"Simple SL","Simple ML","26-L","24-RL","No.6")+" with "+(LocoBrakeMode ? "ML" : "SL")+" Independents\nTCL Sound Enhancement Equipped")
    runOnChat(1)
    timer("clk",100)
    
    #Notch Indicator
    holoCreate(0)
    holoModel(0,"models/jazzie/trains/number_wheel.mdl")
    holoMaterial(0,"trans_train_stuff/cabstuff/indicators/jazzie_notchwheel")
    holoPos(0,CS:toWorld(vec(4.65,8.39,31.55)))
    holoScale(0,vec(0.05,0.07,0.05))
    holoParent(0,CS)
    holoDisableShading(0,1)
}
if(MU_Master){
            
    #MASTER MODE
    local Driver = noentity()
    local PodDriver = Pod:driver()
    if(RemoteControl | RemoteMode){
        if(Active & !PodDriver){
            Driver = owner()
        }
    }else{
        Driver = Pod:driver()
    }
    if(Driver & !DriverQ){
        DriverQ = 1
        runOnKeys(Driver,1)
    }elseif(!Driver & DriverQ){
        DriverQ = 0
        runOnKeys(DriverOld,0)
    }
    DriverOld = Driver
    
    if(keyClk(Driver)){
        local Shifting = Driver:keyPressed(Shift)
        
        UseButton = Driver:keyPressed(Controls["UseButton",string]) & (Routing["UseButton",number] ? Shifting : !Shifting)
        
        if(!UseButton){
        
            local RF = UsingRearSeat ? "ReverserB" : "ReverserF"
            local RB = UsingRearSeat ? "ReverserF" : "ReverserB"
            
            ReverserF = Driver:keyPressed(Controls[RF,string]) & (Routing[RF,number] ? Shifting : !Shifting)
            ReverserB = Driver:keyPressed(Controls[RB,string]) & (Routing[RB,number] ? Shifting : !Shifting)
            
            KeyThrUp = Driver:keyPressed(Controls["IncreaseThrottle",string]) & (Routing["IncreaseThrottle",number] ? Shifting : !Shifting)
            KeyThrDn = Driver:keyPressed(Controls["DecreaseThrottle",string]) & (Routing["DecreaseThrottle",number] ? Shifting : !Shifting)
            
            IncreaseThrottle = 0
            DecreaseThrottle = 0
            
            IncreaseTrainBrake = Driver:keyPressed(Controls["IncreaseTrainBrake",string]) & (Routing["IncreaseTrainBrake",number] ? Shifting : !Shifting)
            DecreaseTrainBrake = Driver:keyPressed(Controls["DecreaseTrainBrake",string]) & (Routing["DecreaseTrainBrake",number] ? Shifting : !Shifting)
            
            IncreaseLocoBrake = Driver:keyPressed(Controls["IncreaseLocoBrake",string]) & (Routing["IncreaseLocoBrake",number] ? Shifting : !Shifting)
            DecreaseLocoBrake = Driver:keyPressed(Controls["DecreaseLocoBrake",string]) & (Routing["DecreaseLocoBrake",number] ? Shifting : !Shifting)
            
            KeyDynUp = Driver:keyPressed(Controls["IncreaseDynamics",string]) & (Routing["IncreaseDynamics",number] ? Shifting : !Shifting)
            KeyDynDn = Driver:keyPressed(Controls["DecreaseDynamics",string]) & (Routing["DecreaseDynamics",number] ? Shifting : !Shifting)
            
            IncreaseDynamics = 0
            DecreaseDynamics = 0
            
            EmergencyBrake = Driver:keyPressed(Controls["EmergencyBrake",string]) & (Routing["EmergencyBrake",number] ? Shifting : !Shifting)
            
            HandBrake = Driver:keyPressed(Controls["HandBrake",string]) & (Routing["HandBrake",number] ? Shifting : !Shifting)
            Sander = Driver:keyPressed(Controls["Sander",string]) & (Routing["Sander",number] ? Shifting : !Shifting)
            
            SelectorUp = Driver:keyPressed(Controls["SelectorUp",string]) & (Routing["SelectorUp",number] ? Shifting : !Shifting)
            SelectorDown = Driver:keyPressed(Controls["SelectorDown",string]) & (Routing["SelectorDown",number] ? Shifting : !Shifting)
            
        }
    }
}else{
    if(DriverQ){
        DriverQ = 0
        runOnKeys(owner(),0)
    }
}
if(clk("clk")){
    local Tick = 1
    Dynamics = SetDynamics>0
    
    #Lever-Based Controlling - This part is different for every control stand!
    if(LeverBased){
        #Initial Control Resets
        
        #Throttle/Dynamics Interlock
        if(!NQ & !DNQ){
            if((SetThrottle==0) & KeyThrDn){
                DecreaseThrottle = 0
                IncreaseDynamics = 1
            }elseif(Dynamics){
                IncreaseDynamics = KeyThrDn
                DecreaseDynamics = KeyThrUp
                
                #IncreaseThrottle = 0
                #DecreaseThrottle = 0
            }else{
                IncreaseThrottle = KeyThrUp
                DecreaseThrottle = KeyThrDn
            }
        }
    }else{
        IncreaseDynamics = KeyDynUp
        DecreaseDynamics = KeyDynDn
        IncreaseThrottle = KeyThrUp
        DecreaseThrottle = KeyThrDn
    }
    
    #CPH - Throttle
    if(!Dynamics){
        if(NotchedThrottle){
            if(IncreaseThrottle & !NQ){
                if(Notch<NumThrottleNotches-1){
                    NQ = 1
                    Notch++
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleNotch)
                }elseif(Notch==NumThrottleNotches-1){
                    NQ = 1
                    Notch++
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleMax)
                }
            }elseif(DecreaseThrottle & !NQ & (Notch>0)){
                if(Notch>1){
                    NQ = 1
                    Notch--
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleNotch)
                }elseif(Notch==1){
                    NQ = 1
                    Notch--
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleIdle)
                }
            }elseif(!IncreaseThrottle & !DecreaseThrottle & NQ){
                NQ = 0
                #soundStop(1)
            }
            SetThrottle = dac(0,100,NumThrottleNotches,Notch)
        }else{
            SetThrottle += (IncreaseThrottle - DecreaseThrottle)*5
            SetThrottle = clamp(SetThrottle,0,100)
            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
        }
    }
    
    #Hump Control
    
    SetHumpMul = ->HumpMultiplier ? HumpMultiplier : 1
    
    #CPH - Dynamic Brakes
    if((SetThrottle==0) & HasDynamics & (abs(SetReverser)==1)){
        if((SetDynamics==0) & IncreaseDynamics & !DNQ){
            DNQ = 1
            DynaNotch = 0
            SetDynamics = 10
            soundStop(1)
            CS:soundPlay(1,2,Lever_PowerHandleCrossover)
            Midshift = 1
        }elseif((SetDynamics==10) & DecreaseDynamics & !DNQ){
            DNQ = 1
            DynaNotch = -1
            SetDynamics = 0
            soundStop(1)
            CS:soundPlay(1,2,Lever_PowerHandleCrossover)
            Midshift = 1
        }
        
        if(Dynamics & NotchedDynamics){
            if(IncreaseDynamics & !DNQ){
                if(DynaNotch<NumThrottleNotches-1){
                    DNQ = 1
                    DynaNotch++
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleNotch)
                }elseif(DynaNotch==NumThrottleNotches-1){
                    DNQ = 1
                    DynaNotch++
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleMax)
                }
            }elseif(DecreaseDynamics & !DNQ & (DynaNotch>-1)){
                DNQ = 1
                DynaNotch--
                soundStop(1)
                CS:soundPlay(1,2,Lever_PowerHandleNotch)
            }
            if(DynaNotch>-1){SetDynamics = dac(10,100,NumThrottleNotches,DynaNotch)}
        }elseif(Dynamics){
            if(IncreaseDynamics & (SetDynamics<100) & !DNQ){
                SetDynamics+=2
                if(SetDynamics>100){
                    SetDynamics=100
                }elseif(SetDynamics>99){
                    DNQ = 1
                    soundStop(1)
                    CS:soundPlay(1,2,Lever_PowerHandleDBMax)
                }
            }elseif(DecreaseDynamics & (SetDynamics>10) & !DNQ){
                SetDynamics-=2
                if(SetDynamics<=10){
                    SetDynamics=10
                    DNQ = 1
                    CS:soundPlay(1,2,Lever_PowerHandleDBMax)
                }
            }
            DynaNotch = adc(10,100,NumThrottleNotches,SetDynamics)
        }
        if(!IncreaseDynamics & !DecreaseDynamics & DNQ){
            DNQ = 0
            Midshift = 0
        }
    }
    
    #Notch Indicator
    if(!Dynamics){
        holoAng(0,CS:toWorld(ang(-47+(Notch*36),0,0)))
    }elseif(Dynamics){
        holoAng(0,CS:toWorld(ang(-83,0,0)))
    }
    
    #Selector
    if((TransitionMode==1) & (TransitionControlType<2)){
        if(SelectorUp & !TransitionQ){
            TransitionQ = 1
            if(SetSelector<4){
                SetSelector++
                soundStop(1)
                CS:soundPlay(1,2,Lever_PowerHandleCrossover)
            }
        }elseif(SelectorDown & !TransitionQ){
            TransitionQ = 1
            if(SetSelector>0){
                SetSelector--
                soundStop(1)
                CS:soundPlay(1,2,Lever_PowerHandleCrossover)
            }
        }elseif(!SelectorUp & !SelectorDown & TransitionQ){
            TransitionQ = 0
        }
        
    }
    
    #Reverser
    
    if((SetThrottle==0) & (!Dynamics)){
        if(ReverserF & !RevQ & (SetReverser<1)){
            RevQ = 1
            SetReverser++
            soundStop(1)
            CS:soundPlay(1,2,(SetReverser==0) ? Lever_ReverserN : Lever_ReverserF)
        }elseif(ReverserB & !RevQ & (SetReverser>-1)){
            RevQ = 1
            SetReverser--
            soundStop(1)
            CS:soundPlay(1,2,(SetReverser==0) ? Lever_ReverserN : Lever_ReverserB)
        }elseif(!ReverserF & !ReverserB & RevQ){
            RevQ = 0
        }
    }
    
    #Train Brakes
    local FSP = FeedPressure*64/90 #Full Service Point
    
    if(AirBrakeMode==4){ #6SL
        #0-Release
        #1-Running
        #2-Holding
        #3-Lap
        #4-Service
        #6-Emergency
        if(IncreaseTrainBrake & (SetBrakeNotch < 4) & !ABQ){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,2,Lever_ABNotch)
            if(SetBrakeNotch==4 & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & (SetBrakeNotch > 0) & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=4
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,2,Lever_ABNotch)
            if(SetBrakeNotch<=1 & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            #soundStop(3)
            ABQ = 0
        }
        switch(SetBrakeNotch){
            case 0, #Release
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 1, #Running
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 2, #Holding
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 4, #Service
                if(SetEqualRes>FSP){
                    SetEqualRes-0.5
                    if(SetEqualRes<FSP){SetEqualRes=FSP}
                }
                break
            default,
                break
        }
    }elseif(AirBrakeMode==3){ #24RL
        #0-Release
        #1-Running
        #2-First Service
        #3-Lap
        #4-Service
        #6-Emergency
        if(IncreaseTrainBrake & (SetBrakeNotch < 4) & !ABQ){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,2,Lever_ABNotch)
            if(SetBrakeNotch==4 & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }elseif(SetBrakeNotch==2 & (SetEqualRes>(FeedPressure-InitialReduction))){
                SetEqualRes = FeedPressure-InitialReduction
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & (SetBrakeNotch > 0) & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=4
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,2,Lever_ABNotch)
            if(SetBrakeNotch<=1 & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            #soundStop(3)
            ABQ = 0
        }
        switch(SetBrakeNotch){
            case 0, #Release
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 1, #Running
                if(SetEqualRes<FeedPressure){
                    SetEqualRes+=0.5
                    if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
                }
                break
            case 4, #Service
                if(SetEqualRes>FSP){
                    SetEqualRes-=0.5
                    if(SetEqualRes<FSP){SetEqualRes=FSP}
                }
                break
            default,
                break
        }
        
    }elseif(AirBrakeMode==2){#26L
    #0-Release
    #1-Minimum Reduction / Service
    #2-Suppression
    #6-Emergency
        if(IncreaseTrainBrake){
            if(!ABQ){
                ABQ = 1
                if(SetBrakeNotch==0){
                    Lock_26L = 1
                    SetBrakeNotch=1
                    soundStop(2)
                    CS:soundPlay(2,2,Lever_ABNotch)
                    CS:soundPlay(3,10,AirBrakeApplyInitial)
                    SetEqualRes = FeedPressure-InitialReduction
                }elseif((SetBrakeNotch==1) & (SetEqualRes>FSP)){
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABMove)
                    HissQ = 1
                    CS:soundPlay(3,10,AirBrakeApply)
                    timer("EndHiss",6000)
                }elseif((SetBrakeNotch==1) & (SetEqualRes==FSP)){
                    SetBrakeNotch=2
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABSuppressionOn)
                    Suppression = 1
                }
            }
            if((SetBrakeNotch==1) & (SetEqualRes>FSP) & !Lock_26L){
                SetEqualRes-=0.5
                if(SetEqualRes<FSP){SetEqualRes=FSP}
            }
        }elseif(DecreaseTrainBrake){
            if(!ABQ){
                ABQ = 1
                if((SetBrakeNotch==1) & (SetEqualRes==(FeedPressure-InitialReduction))){
                    SetBrakeNotch=0
                    soundStop(2)
                    CS:soundPlay(2,2,Lever_ABRelease)
                    SetEqualRes = FeedPressure
                    if(!HissQ){
                        HissQ = 1
                        CS:soundPlay(3,10,AirBrakeRelease)
                        timer("EndHiss",6000)
                    }
                }elseif((SetBrakeNotch==1) & (SetEqualRes<FeedPressure)){
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABMove)
                    if(!HissQ & (SetEqualRes<FeedPressure)){
                        HissQ = 1
                        CS:soundPlay(3,10,AirBrakeRelease)
                        timer("EndHiss",6000)
                    }
                }elseif((SetBrakeNotch==2)){
                    Lock_26L = 1
                    SetBrakeNotch=1
                    soundStop(2)
                    CS:soundPlay(2,5,Lever_ABSuppressionOff)
                    Suppression = 0
                }elseif(SetBrakeNotch==6){
                    SetBrakeNotch=2
                    soundStop(2)
                    soundStop(3)
                    CS:soundPlay(2,10,Lever_ABEmergencyRecover)
                    Suppression = 1
                }
                
            }
            if((SetBrakeNotch==1) & (SetEqualRes<(FeedPressure-InitialReduction)) & !Lock_26L){
                SetEqualRes+= 1.5 #Triple Release Speed
                if(SetEqualRes>=(FeedPressure-InitialReduction)){
                    SetEqualRes = FeedPressure - InitialReduction
                    ABQ = 0
                    }
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
            Lock_26L = 0
        }
    }elseif(AirBrakeMode==1){#Simple Manual Lapping
    #0-Release
    #1-Lap
    #2-Apply
    #6-Emergency
        if(IncreaseTrainBrake & !ABQ & (SetBrakeNotch<2)){
            ABQ = 1
            SetBrakeNotch++
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetBrakeNotch==2) & (SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & !ABQ & (SetBrakeNotch>0)){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch=2
                soundStop(2)
                CS:soundPlay(2,10,Lever_ABEmergencyRecover)
            }else{
                SetBrakeNotch--
            }
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetBrakeNotch==0) & (SetEqualRes<FeedPressure) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeRelease)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
        }
        

        if((SetBrakeNotch==2) & (SetEqualRes>FSP)){
            SetEqualRes-=0.25
            if(SetEqualRes<FSP){SetEqualRes=FSP}
        }elseif((SetBrakeNotch==0) & (SetEqualRes<FeedPressure)){
            SetEqualRes+=0.25
            if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
        }
        
    }else{#Simple Self Lapping
            
    #Lol what notches?
    #0-Service Region
    #6-Emergency
        if(IncreaseTrainBrake & !ABQ){
            ABQ = 1
            soundStop(2)
            CS:soundPlay(2,5,Lever_ABMove)
            if((SetEqualRes>FSP) & !HissQ){
                HissQ = 1
                CS:soundPlay(3,10,AirBrakeApply)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseTrainBrake & !ABQ){
            ABQ = 1
            if(SetBrakeNotch==6){
                SetBrakeNotch = 0
                soundStop(2)
                CS:soundPlay(2,10,Lever_ABEmergencyRecover)
            }else{
                soundStop(2)
                CS:soundPlay(2,5,Lever_ABMove)
                if((SetEqualRes<FeedPressure) & !HissQ){
                    HissQ = 1
                    CS:soundPlay(3,10,AirBrakeRelease)
                    timer("EndHiss",6000)
                }
            }
        }elseif(!IncreaseTrainBrake & !DecreaseTrainBrake & ABQ){
            ABQ = 0
        }

            
        if(IncreaseTrainBrake & (SetEqualRes>FSP)){
            SetEqualRes-=0.25
            BleedPressure = Bleeding ? 0 : FeedPressure
            if(SetEqualRes<FSP){SetEqualRes=FSP}
        }elseif(DecreaseTrainBrake & (SetEqualRes<FeedPressure)){
            SetEqualRes+=0.25
            if(SetEqualRes>FeedPressure){SetEqualRes=FeedPressure}
        }
        
    }
    
    #Locomotive Brakes
    if(!LocoBrakeMode){ #Self Lapping
        if(IncreaseLocoBrake & (SetLocoBrakePressure<60)){
            #if(BleedPressure<72){BleedPressure=72}
            if(!LBQ){LBQ=1}
            SetLocoBrakePressure+=4
            if(SetLocoBrakePressure>60){
                SetLocoBrakePressure = 60
            }
            if(SetLocoBrakePressure==60 & !LHissQ){
                LHissQ = 1
                soundStop(2)
                CS:soundPlay(2,10,LocoBrakeFull)
                timer("EndHiss",500)
            }
        }elseif(DecreaseLocoBrake & (SetLocoBrakePressure>0)){
            if(!LBQ){LBQ=1}
            SetLocoBrakePressure-=2
            if(SetLocoBrakePressure<0){SetLocoBrakePressure = 0}
            if(!LHissQ){
                LHissQ = 1
                soundStop(2)
                CS:soundPlay(2,10,(SetLocoBrakePressure>30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseLocoBrake & (SetLocoBrakePressure==0) & !LBQ){
            LBQ = 1
            SetBleeding = 1
            if((SetEqualRes<FeedPressure) & !LHissQ){
                LHissQ = 1
                soundStop(2)
                CS:soundPlay(2,10,(SetEqualRes<30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseLocoBrake & !DecreaseLocoBrake & LBQ){
            LBQ = 0
            SetBleeding = 0
        }

    }else{ #Manual Lapping
            
        if(IncreaseLocoBrake & !LBQ & (SetLocoBrakeNotch<2)){
            LBQ = 1
            SetLocoBrakeNotch++
            if(SetLocoBrakeNotch==2){
                soundStop(2)
                CS:soundPlay(2,10,LocoBrakeFull)
                #BleedPressure = 72
            }
        }elseif(DecreaseLocoBrake & !LBQ & (SetLocoBrakeNotch>0)){
            LBQ = 1
            SetLocoBrakeNotch--
            if((SetLocoBrakeNotch<=0) & (SetLocoBrakePressure>0) & !LHissQ){
                LHissQ = 1
                soundStop(2)
                CS:soundPlay(2,10,(SetLocoBrakePressure>30) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(DecreaseLocoBrake & !LBQ & (SetLocoBrakeNotch==0)){
            LBQ = 1
            SetBleeding = 1
            if((SetLocoBrakeNotch<=0) & (SetEqualRes<FeedPressure) & !LHissQ){
                LHissQ = 1
                soundStop(2)
                CS:soundPlay(2,10,(SetEqualRes<60) ? LocoBrakeReleaseHard : LocoBrakeReleaseSoft)
                timer("EndHiss",6000)
            }
        }elseif(!IncreaseLocoBrake & !DecreaseLocoBrake & LBQ){
            LBQ = 0
            SetBleeding = 0
            
        }
       
        if(SetBleeding & (SetLocoBrakePressure>0)){
            SetLocoBrakePressure-=4
            if(SetLocoBrakePressure<0){SetLocoBrakePressure=0}
        }elseif((SetLocoBrakeNotch==2) & (SetLocoBrakePressure<60)){
            SetLocoBrakePressure+=2
            if(SetLocoBrakePressure>60){SetLocoBrakePressure=60}
        }elseif((SetLocoBrakeNotch<=0) & (SetLocoBrakePressure>0)){
            SetLocoBrakePressure-=2
            if(SetLocoBrakePressure<0){SetLocoBrakePressure=0}
        }
        
    }
    
    #Emergency Brake
    
    if(EmergencyBrake | EmBrake){
        
        if(SetBrakeNotch<6){
            CS:soundPlay(3,0,Lever_ABEmergency)
        }
        
        SetEqualRes = 0
        SetThrottle = 0
        Notch = 0
        SetDynamics = 0
        DynaNotch = 0
        SetBrakeNotch = 6
    }
        
    #Sander Button Sounds
    
    if(ForceSand & !SanderQ){
        SanderQ = 1
        CS:soundPlay(4,2,ButtonMomOn)
    }elseif(!ForceSand & SanderQ){
        SanderQ = 0
        CS:soundPlay(4,2,ButtonMomOff)
    }
    
    #Horn Button and Air Sounds
    
    if(Horn1 & !HornQ){
        HornQ = 1
        CS:soundPlay(4,2,ButtonMomOn)
        CS:soundPlay(5,0,HornAirOn)
    }elseif(!Horn1 & HornQ){
        HornQ = 0
        CS:soundPlay(4,2,ButtonMomOff)
        CS:soundPlay(5,5,HornAirOff)
    }
        
    #Control Stand
    
    #Reverser
    CS:setBodygroup(1,MU_Master ? SetReverser+2 : 0)
    #Throttle and Dynamics
    CS:setBodygroup(2,Midshift ? 9 : Dynamics ? adc(10,100,8,SetDynamics)+10 : adc(0,100,8,SetThrottle))
    #Dynamic Brakes
    #CS:setBodygroup(3,Dynamics ? adc(10,100,8,SetDynamics)+1 : 0)
    #Train Brake
    local TBB = 0
    switch(AirBrakeMode){
        case 4, #6SL
            TBB = select(SetBrakeNotch+1,0,2,4,7,10,0,11)
            break
        case 3, #24RL
            TBB = select(SetBrakeNotch+1,0,2,4,7,10,0,11)
            break
        case 2, #26L
            TBB = select(SetBrakeNotch+1,0,adc(0,FeedPressure-InitialReduction,6,FeedPressure-InitialReduction-SetEqualRes)+2,10,0,0,0,11)
            break
        case 1, #SML
            TBB = select(SetBrakeNotch+1,0,5,10,0,0,0,11)
            break
        case 0, #SSL
            TBB = select(SetBrakeNotch+1,adc(0,FeedPressure,10,FeedPressure-SetEqualRes),0,0,0,0,0,11)
            break
    }
    CS:setBodygroup(4,TBB)
    #Loco Brake
    local LBB = 0
    if(LocoBrakeMode){ #ML
        LBB = SetBleeding ? 0 : select(SetLocoBrakeNotch+1,1,4,7)
    }else{ #SL
        LBB = SetBleeding ? 0 : adc(0,72,6,SetLocoBrakePressure)+1
    }
    CS:setBodygroup(5,LBB)
    #Horn 1
    
    CS:setBodygroup(6,Horn1)
    
    #Horn 2 Unused
    #CS:setBodygroup(1,MU_Master ? SetReverser+2 : 0)
    
    #Bell On/Off
    #See Bell Toggle
    
    #Sander
    CS:setBodygroup(9,ForceSand)
    
    
    
    timer("clk",50)
}elseif(clk("EndHiss")){
    HissQ = 0
    LHissQ = 0
}elseif(clk("PenaltyTimeout")){
    PenaltyBrake = 0   
}

local InRelease = ((AirBrakeMode==3)|(AirBrakeMode==4)) & (SetBrakeNotch==0)
if(InRelease & !WarningHissQ){
    WarningHissQ = 1
    CS:soundPlay(3,0,WarningHiss)
}elseif(!InRelease & WarningHissQ){
    WarningHissQ = 0
    soundStop(3,0.125)
}

CtlTable["SetReverser",number] = SetReverser
CtlTable["SetThrottle",number] = SetThrottle
CtlTable["SetDynamics",number] = SetDynamics
CtlTable["SetSelector",number] = SetSelector
CtlTable["SetBrakeNotch",number] = SetBrakeNotch
CtlTable["SetEqualRes",number] = SetEqualRes
CtlTable["SetHolding",number] = (AirBrakeMode==4)&(SetBrakeNotch==2)
CtlTable["Overcharge",number] = (SetBrakeNotch==0) & ((AirBrakeMode==3)|(AirBrakeMode==4))
CtlTable["FRC",number] = FullReleaseCock
#CtlTable["SetLocoBrakeNotch",number] = SetLocoBrakeNotch
CtlTable["SetLocoBrakePressure",number] = SetLocoBrakePressure
CtlTable["SetBleeding",number] = SetBleeding
CtlTable["SetHumpMultiplier",number] = SetHumpMul
CtlTable["SetFeedPressure",number] = FeedPressure

CtlTable["EmergencyBrake",number] = EmergencyBrake
CtlTable["HandBrake",number] = HandBrake
CtlTable["Sander",number] = Sander

if(chatClk(owner())|chatClk(Pod:driver())){
    local Speaker = lastSpoke()
    local Sentence = Speaker:lastSaid():explode(" ")
    local CMD_PreNum = Sentence[1,string]
    local CMD_S = CMD_PreNum:explode(":")
    local CMD = CMD_S[1,string]
    local NumberGiven = CMD_S[2,string]
    local Num = Number:trim()
    
    local OnRun = 1#(On1 & Run)|(On2 & Run_DPM)
    
    
    if((NumberGiven==Num) | NumberGiven==""){
        switch(CMD){
            case ".controls",
                if(MU_Master){
                    for(K=1,ControlPrintOrder:count()){
                        local ControlName = ControlPrintOrder[K,string]
                        local X = Controls[ControlName,string]
                        if(Routing[ControlName,number]){
                            Speaker:printProper(ControlName + ": Shift + " + X)
                        }else{
                            Speaker:printProper(ControlName + ": " + X)
                        }
                    }
                }
                hideChat(1)
                break
            case ".reverser",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(SetThrottle>0){
                        Speaker:printProper("[RLCPT] Throttle is On!")
                    }else{
                        SetReverser = sign(Sentence[2,string]:toNumber())
                        Speaker:printProper("[RLCPT] Reverser set to " + SetReverser:toString() + ".")
                    }
                }
                hideChat(1)
                break
            case ".throttle",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(HEP_Notch==1){
                        Speaker:printProper("[RLCPT] HEP Mode is in Standby!")
                    }else{
                        if(NotchedThrottle){
                            SetThrottle = clamp(dac(0,100,NumThrottleNotches,adc(0,100,NumThrottleNotches,Sentence[2,string]:toNumber())),0,100)
                            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
                        }else{
                            SetThrottle = clamp(Sentence[2,string]:toNumber(),0,100)
                            Notch = adc(0,100,NumThrottleNotches,SetThrottle)
                        }
                        Speaker:printProper("[RLCPT] Throttle set to " + SetThrottle + "%.")
                    }
                }
                hideChat(1)
                break
            case ".notch",
                if(MU_Master & OnRun){
                    if(Dynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are On!")
                    }elseif(HEP_Notch==1){
                        Speaker:printProper("[RLCPT] HEP Mode is in Standby!")
                    }else{
                        if(NotchedThrottle){
                            Notch = clamp(round(Sentence[2,string]:toNumber()),0,NumThrottleNotches)
                            SetThrottle = dac(0,100,NumThrottleNotches,Notch)
                            Speaker:printProper("[RLCPT] Throttle set to Notch " + Notch + ".")
                            
                        }else{
                            Speaker:printProper("[RLCPT] Throttle has no Notches!")
                        }
                        
                    }
                }
                hideChat(1)
                break
            case ".dynamics",
                if(MU_Master & OnRun){
                    if(!HasDynamics){
                        Speaker:printProper("[RLCPT] Dynamic Brakes are not Enabled!")
                    }elseif(!abs(SetReverser)){
                        Speaker:printProper("[RLCPT] Reverser is not Engaged!")
                    }elseif(SetThrottle>0){
                        Speaker:printProper("[RLCPT] Throttle is not in Idle!")
                    }else{
                        local Set = clamp(Sentence[2,string]:toNumber(),0,100)
                        SetDynamics = (Set<10) ? 0 : Set
                        DynaNotch = (SetDynamics==0) ? -1 : adc(10,100,NumThrottleNotches,SetDynamics)
                        
                        #Dynamics = DynamicBrakingPower>0
                        
                        Speaker:printProper("[RLCPT] Dynamic Brake set to " + SetDynamics + "%.")
                    }
                }
                hideChat(1)
                break
            case ".dynanotch",
                if(MU_Master & OnRun){
                    if(NotchedDynamics){
                        if(!HasDynamics){
                            Speaker:printProper("[RLCPT] Dynamic Brakes are not Enabled!")
                        }elseif(!abs(SetReverser)){
                            Speaker:printProper("[RLCPT] Reverser is not Engaged!")
                        }elseif(SetThrottle>0){
                            Speaker:printProper("[RLCPT] Throttle is not in Idle!")
                        }else{
                            local Set = clamp(round(Sentence[2,string]:toNumber()),-1,NumThrottleNotches)
                            DynaNotch = Set
                            SetDynamics = (Set==-1) ? 0 : dac(10,100,NumThrottleNotches,DynaNotch)
                            
                            #Dynamics = DynamicBrakingPower>0
                            
                            Speaker:printProper("[RLCPT] Dynamic Brake set to Notch " + DynaNotch + " (" + SetDynamics + "%).")
                        }
                    }else{
                        Speaker:printProper("[RLCPT] Dynamic Brakes are not Notched!")
                    }
                }
                hideChat(1)
                break
            case ".abset",
                if(MU_Master & OnRun){
                    local What = Sentence[2,string]
                    if(What==""){
                        SetEqualRes = 0
                    				SetThrottle = 0
                    				Notch = 0
                    				SetDynamics = 0
                    				SetBrakeNotch = 6
                        Speaker:printProper("[RLCPT] Setting Emergency Brake application!")
                    }else{
                        local Set = clamp(What:toNumber(),0,FeedPressure*64/90)
                        switch(AirBrakeMode){
                            case 4, #6SL
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 1
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 3
                                }
                                break
                            case 3, #24RL
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 1
                                }elseif(Set<InitialReduction){
                                    SetEqualRes = FeedPressure - InitialReduction
                                    SetBrakeNotch = 2
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 3
                                }
                                break
                            case 2, #26L
                                if(Set==0){
                                    SetEqualRes = FeedPressure
                                    SetBrakeNotch = 0
                                }elseif(Set<InitialReduction){
                                    SetEqualRes = FeedPressure - InitialReduction
                                    SetBrakeNotch = 1
                                }else{
                                    SetEqualRes = FeedPressure - Set
                                    SetBrakeNotch = 1
                                }
                                break
                            case 1, #SML
                                SetBrakeNotch = 1
                                SetEqualRes = FeedPressure - Set
                                break
                            case 0, #SSL
                                SetBrakeNotch = 0
                                SetEqualRes = FeedPressure - Set
                                break
                        }
                        Speaker:printProper("[RLCPT] Setting " + (FeedPressure-SetEqualRes) + "lb application on train.")
                    }
                }
                hideChat(1)
                break
			case ".brake",
				if(MU_Master & OnRun){
					SetEqualRes = 0
					SetThrottle = 0
					Notch = 0
					SetDynamics = 0
					SetBrakeNotch = 6
					Speaker:printProper("[RLCPT] Setting Emergency Brake application!")
				}
				hideChat(1)
				break
            case ".lbset",
                local Set = clamp(Sentence[2,string]:toNumber(),0,72)
                local ML = (AirBrakeMode==1)|(AirBrakeMode==3)|(AirBrakeMode==4)
                SetLocoBrakePressure = Set
                if(ML){SetLocoBrakeNotch=1}
                Speaker:printProper("[RLCPT] Setting " + (SetLocoBrakePressure) + "lb application on locomotive(s).")
                
                hideChat(1)
                break
            case ".remotemode",
                if(MU_Master){
                    RemoteMode = sign(Sentence[2,string]:toNumber())
                    Speaker:printProper("[RLCPT] Remote Control Mode set to " + RemoteMode + ".")
                    hideChat(1)
                }
                break
            case ".feedvalve",
                if(Sentence[2,string]==""){
                    print("[RLCPT] Your Feed Valve is currently set to "+FeedPressure+" PSI.")
                }else{
                    FeedPressure = clamp(Sentence[2,string]:toNumber(),70,120)
                    print("[RLCPT] Your Feed Valve is now set to "+FeedPressure+" PSI.")
                }
                hideChat(1)
                break
            case ".fullreleasecock",
                if(AirBrakeMode==3){
                    if(Sentence[2,string]==""){
                        FullReleaseCock = 0
                    }else{
                        FullReleaseCock = Sentence[2,string]:toNumber()
                    }
                    print("[RLCPT] Your Full Release Cock is now set to " + (FullReleaseCock ? "MR (1)." : "FV (0)."))
                }else{
                    print("[RLCPT] You don't have a Full Release Cock!")
                }
                
                hideChat(1)
                break
            default,
                if(CMD:index(1)=="."){
                    local CMDL = CMD:lower()
                    if("abcdefghijklmnopqrstuvwxyz":find(CMDL:index(2))){
                        hideChat(1)
                    }
                }
                break
        }
    }
    
}
if(LightToggle & ~LightToggle){
    Light = !Light
    CS:soundPlay(4,1,MiscSwitch)
}
if(MiscToggle & ~MiscToggle){
    Misc = !Misc
    CS:soundPlay(4,1,MiscSwitch)
}
if(BellToggle & !BellQ){
    CS:soundPlay(6,5,Bell ? BellValveOff : BellValveOn)
    BellQ = 1
    if(Bell){
        CS:setBodygroup(7,1)
    }else{
        CS:setBodygroup(8,1)
    }
}elseif(!BellToggle & BellQ){
    Bell = !Bell
    BellQ = 0
    CS:setBodygroup(7,0)
    CS:setBodygroup(8,0)
    
}
if(BellTrigger){
    if(Horn1 & !Bell){
        Bell = 1
        CS:soundPlay(6,5,BellTriggerOn)
    }
}
